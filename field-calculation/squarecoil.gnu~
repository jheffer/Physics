# One square coil
# Should look like this, Eq. 9:
# http://www.physicspages.com/2013/04/11/magnetic-field-of-square-current-loop/

set title "Square coil: 5 cm sides, 10 turns, 10 mA\n(y = 0, z = 0)"
set xlabel "Position x/m"
set ylabel "Magnetic field strength/mG"

set output "squarecoil.ps"
set size 1.0, 1/1.61803398875
set terminal postscript portrait enhanced solid lw 1 "CM Roman, Times New Roman, Serif" 16

Bscale = 1E3*1E4 # convert T => mG
x1 = 0 # coil position
a = 0.05 # coil size/m
u0 = 4*pi*1e-7 # permability free space/(Tm/A)
I = 0.010 # current/A
N = 10

# coil positions
set arrow from x1,graph 0 to x1, graph 1 nohead lt rgb "#999999"

Bx(x) = Bscale * (4*a**2*u0*I) / (pi*sqrt(2*a**2 + 4*x**2)*(a**2 + 4*x**2))
By(x) = 0
Bz(x) = 0

plot \
Bx(x) t "B_{x(theory)}" lt rgb "red",\
"squarecoil.txt" u 1:(sqrt($4**2+$5**2+$6**2)*Bscale) t "|B|" pt 7 ps 0.2,\
"squarecoil.txt" u 1:($4*Bscale) t "B_x" pt 7 ps 0.2 lt rgb "red",\
"squarecoil.txt" u 1:($5*Bscale) t "B_y" pt 7 ps 0.2 lt rgb "green",\
"squarecoil.txt" u 1:($6*Bscale) t "B_z" pt 7 ps 0.2 lt rgb "blue"
