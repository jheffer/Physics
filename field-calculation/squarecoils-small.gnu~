# Square coils
# Should look like this, twice, Eq. 9:
# http://www.physicspages.com/2013/04/11/magnetic-field-of-square-current-loop/

set title "Square coils: 20 cm spacing, 5 cm sides, 100 turns, 1 A\n(y = 0, z = 0)"
set xlabel "Position x/m"
set ylabel "Magnetic field strength/mG"
set xrange [-0.01:0.01]

set output "squarecoils-small.ps"
set size 1.0, 1/1.61803398875
set terminal postscript portrait enhanced solid lw 1 "CM Roman, Times New Roman, Serif" 16

Bscale = 1E4 # convert T => G
x1 = 0 # coil position
a = 0.05 # coil size/m
u0 = 4*pi*1e-7 # permability free space/(Tm/A)
I = 1 # current/A
N = 100
S = 0.2 # coil spacing/m

# coil positions
x1 = -S/2
x2 = +S/2
set arrow from x1,graph 0 to x1, graph 1 nohead lt rgb "#999999"
set arrow from x2,graph 0 to x2, graph 1 nohead lt rgb "#999999"

B1x(x) = Bscale * (4*a**2*u0*I*N) / (pi*sqrt(2*a**2 + 4*(x-x1)**2)*(a**2 + 4*(x-x1)**2))
B2x(x) = Bscale * (4*a**2*u0*I*N) / (pi*sqrt(2*a**2 + 4*(x-x2)**2)*(a**2 + 4*(x-x2)**2))
Bx(x) = B1x(x) + B2x(x)

plot \
Bx(x) t "B_{x(theory)}" lt rgb "red",\
"squarecoils.txt" u 1:(sqrt($4**2+$5**2+$6**2)*Bscale) t "|B|" pt 7 ps 0.4,\
"squarecoils.txt" u 1:($4*Bscale) t "B_x" pt 7 ps 0.4 lt rgb "red",\
"squarecoils.txt" u 1:($5*Bscale) t "B_y" pt 7 ps 0.4 lt rgb "green",\
"squarecoils.txt" u 1:($6*Bscale) t "B_z" pt 7 ps 0.4 lt rgb "blue"
